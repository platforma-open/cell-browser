ll := import("@platforma-sdk/workflow-tengo:ll")
pt := import("@platforma-sdk/workflow-tengo:pt")
wf := import("@platforma-sdk/workflow-tengo:workflow")
xsv := import("@platforma-sdk/workflow-tengo:pframes.xsv")
slices := import("@platforma-sdk/workflow-tengo:slices")
pFrames := import("@platforma-sdk/workflow-tengo:pframes")
annotations := import("@platforma-sdk/workflow-tengo:annotations")

util := import(":util")

// Input headers
SAMPLE_KEY_HEADER := "sample_key"
CELL_KEY_HEADER := "cell_key"
GENE_KEY_HEADER := "gene_key"
// States headers
ANNOTATION_COUNT_HEADER := "annotation_count"
ANNOTATION_FRACTION_HEADER := "annotation_fractions"
CELL_COUNT_HEADER := "cell_count"
CELL_FRACTION_HEADER := "cell_fractions"
// Result file
ANNOTATIONS_STATS_FILE := "annotation_stats.tsv"
ANNOTATIONS_STATS_BY_SAMPLE_FILE := "annotation_stats_by_sample.tsv"

convertMapToPFrame := func(mapEntities) {
	pf := pFrames.pFrameBuilder()
	for k, v in mapEntities {
		pf.add(k, v.spec, v.data)
	}
	return pf.build()
}

convertColumnSpecToAxisSpec := func(spec) {
	return {
		type: spec.valueType,
		name: spec.name,
		domain: spec.domain,
		annotations: spec.annotations
	}
}

createColumnSpec := func(type, name, label, axesSpec) {
	blockId := wf.getBlockId()
	return {
		kind: "PColumn",
		valueType: type,
		name: "pl7.app/clonotypeAnnotation/" + name,
		domain: {
			"pl7.app/clonotypeAnnotationRunId": blockId
		},
		axesSpec: axesSpec,
		annotations: {
			"pl7.app/label": label
		}
	}
}

createStatsDataAndSpecByAnnotations := func(file, axes) {
	axesSpec := slices.map(axes, func(v) { return v.spec })
	importSpec := {
		axes: axes,
		columns: [
			{
				column: ANNOTATION_COUNT_HEADER,
				spec: createColumnSpec("Long", "stats/" + ANNOTATION_COUNT_HEADER, "Total Count of Annotations", axesSpec)
			},
			{
				column: CELL_COUNT_HEADER,
				spec: createColumnSpec("Long", "stats/" + CELL_COUNT_HEADER, "Total Number of Cells", axesSpec)
			},
			{
				column: ANNOTATION_FRACTION_HEADER,
				spec: createColumnSpec("Double", "stats/" + ANNOTATION_FRACTION_HEADER, "Fraction of Annotations", axesSpec)
			},
			{
				column: CELL_FRACTION_HEADER,
				spec: createColumnSpec("Double", "stats/" + CELL_FRACTION_HEADER, "Fraction of Cells", axesSpec)
			}
		],
		storageFormat: "Binary",
		partitionKeyLength: len(axes) - 1
	}

	return xsv.importFile(file, "tsv", importSpec, { splitDataAndSpec: true })
}

computeAnnotationsStats := func(inputs) {
	annotations := inputs.annotations
	sampleAxisSpec := annotations.spec.axesSpec[0]
	cellIdAxisSpec := annotations.spec.axesSpec[1]

	statsFileBuilder := pFrames.tsvFileBuilder()
	statsFileBuilder.setAxisHeader(sampleAxisSpec, SAMPLE_KEY_HEADER)
	statsFileBuilder.setAxisHeader(cellIdAxisSpec, CELL_KEY_HEADER)
	statsFileBuilder.add(annotations, { header: annotations.column })
	statsFile := statsFileBuilder.build()

	wf := pt.workflow()
	df := wf.frame(statsFile, { format: "tsv" }).
		select(SAMPLE_KEY_HEADER, CELL_KEY_HEADER, annotations.column)
		
	df.groupBy(annotations.column).
		agg(
			pt.col(annotations.column).count().alias(ANNOTATION_COUNT_HEADER),
			pt.col(CELL_KEY_HEADER).count().alias(CELL_COUNT_HEADER)
		).
		withColumns(
			pt.col(ANNOTATION_COUNT_HEADER).
				truediv(pt.col(ANNOTATION_COUNT_HEADER).sum()).
				alias(ANNOTATION_FRACTION_HEADER),
			pt.col(CELL_COUNT_HEADER).
				truediv(pt.col(CELL_COUNT_HEADER).sum()).
				alias(CELL_FRACTION_HEADER)
		).
		save(ANNOTATIONS_STATS_FILE)

	df.groupBy(SAMPLE_KEY_HEADER, annotations.column).
		agg(
			pt.col(annotations.column).count().alias(ANNOTATION_COUNT_HEADER),
			pt.col(CELL_KEY_HEADER).count().alias(CELL_COUNT_HEADER)
		).
		withColumns(
			pt.col(ANNOTATION_COUNT_HEADER).
				truediv(pt.col(ANNOTATION_COUNT_HEADER).sum().over(SAMPLE_KEY_HEADER)).
				alias(ANNOTATION_FRACTION_HEADER),
			pt.col(CELL_COUNT_HEADER).
				truediv(pt.col(CELL_COUNT_HEADER).sum().over(SAMPLE_KEY_HEADER)).
				alias(CELL_FRACTION_HEADER)
		).
		save(ANNOTATIONS_STATS_BY_SAMPLE_FILE)

	wfResult := wf.run()

	annotationsAxis := { column: annotations.column, spec: convertColumnSpecToAxisSpec(annotations.spec) }	
	statsDataAndSpec := createStatsDataAndSpecByAnnotations(
		wfResult.getFile(ANNOTATIONS_STATS_FILE),
		[annotationsAxis]
	)
	statsDataAndSpecBySample := createStatsDataAndSpecByAnnotations(
		wfResult.getFile(ANNOTATIONS_STATS_BY_SAMPLE_FILE),
		[{ column: SAMPLE_KEY_HEADER, spec: sampleAxisSpec }, annotationsAxis]
	)

	return {
		annotationStatsPf: convertMapToPFrame(statsDataAndSpec),
		annotationStatsBySamplePf: convertMapToPFrame(statsDataAndSpecBySample)
	}
}

export ll.toStrict({
	computeAnnotationsStats: computeAnnotationsStats
})
